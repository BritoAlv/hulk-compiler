Program -> Decls Expr semicolon.
Decls -> FuncDecl Decls | TypeDecl Decls | ProtocolDecl Decls | .

ProtocolDecl -> protocol id OptExtension lbrace ProtocolElems rbrace.
OptExtension -> extends id | .
ProtocolElems -> MethodSignature ProtocolElems | .
MethodSignature -> id lparen TypedParamList rparen colon id semicolon.
TypedParamList -> id colon id TypedParamTail | .
TypedParamTail -> comma id colon id TypedParamTail | .

TypeDecl -> type id OptParams OptInheritance lbrace TypeElems rbrace .

OptParams -> lparen ParamList rparen | .
OptInheritance -> inherits id OptArgs | .
OptArgs -> lparen ArgList rparen | .

TypeElems -> AttributeDecl TypeElems | MethodDecl TypeElems | .
AttributeDecl -> Assignment  semicolon .
MethodDecl -> id lparen ParamList rparen OptType FuncBody .
ParamList -> id OptType ParamTail | .
ParamTail -> comma id OptType ParamTail | .
FuncBody -> arrow Expr semicolon | BlockExpr semicolon .

Assignment -> id OptType equal Expr .
OptType -> colon id | .

FuncDecl -> function MethodDecl .

Expr -> BlockExpr | IfExpr | WhileExpr | ForExpr | LetExpr | DestrucExpr | VectorExpr | NewExpr | As .

LetExpr -> let Assignment AssignmentList in Expr .
AssignmentList -> comma Assignment AssignmentList | .

BlockExpr -> lbrace ExprList rbrace .
ExprList -> Expr semicolon ExprTail .
ExprTail -> Expr semicolon ExprTail | .

IfExpr -> if lparen Expr rparen Expr OptElif else Expr .
OptElif -> elif lparen Expr rparen Expr OptElif | .

WhileExpr -> while lparen Expr rparen Expr .
ForExpr -> for lparen id in Expr rparen Expr .

DestrucExpr -> Primary destrucOp Expr .

VectorExpr -> lbracket VectorElems rbracket | lbracket Expr doubleOr id in Expr rbracket .
VectorElems -> Expr VectorTail | .
VectorTail -> comma Expr VectorTail | .

NewExpr -> new CallList .

As -> LogicOr asOp id | LogicOr .
LogicOr -> LogicOr or LogicAnd | LogicAnd .
LogicAnd -> LogicAnd and Equality | Equality .
Equality -> Equality doubleEqual Comparison | Equality notEqual Comparison | Comparison .
Comparison -> Comparison greater Is | Comparison greaterEq Is | Comparison less Is | Comparison lessEq Is | Is .
Is -> Is isOp Str | Str .
Str -> Str StrOp Term | Term .
StrOp -> at | doubleAt .
Term -> Term plus Factor | Term minus Factor | Factor .

Factor -> Factor star Mod | Factor div Mod | Mod .

Mod -> Mod modOp Power | Power .

Power -> Unary powerOp Power | Unary .

Unary -> minus Primary | not Primary | Primary.

Primary -> false | true | number | string | CallList .

CallList -> CallList dot id | CallList lparen ArgList rparen | CallList lbracket Expr rbracket | self | base | id | lparen Expr rparen .

ArgList -> Expr ArgTail | .

ArgTail -> comma Expr ArgTail | .